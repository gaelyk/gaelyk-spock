apply plugin: "groovy"
apply plugin: "idea"
apply plugin: "eclipse"
apply plugin: "maven"
apply plugin: "nexus"

def compatibilityVersion = 1.6
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

archivesBaseName = "gaelyk-spock"
version = "0.3.2-SNAPSHOT"
group = 'org.gaelyk'

buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.2'
	}
}

repositories {
	mavenCentral()
}

dependencies {
	def gaeVersion = "1.7.3"
	groovy "org.codehaus.groovy:groovy:1.8.6"
	compile "org.spockframework:spock-core:0.7-groovy-1.8", {
		exclude group: 'org.codehaus.groovy'
	}
	compile "org.gaelyk:gaelyk:1.1", {
		exclude group: 'org.codehaus.groovy'
	}
	compile "com.google.appengine:appengine-api-1.0-sdk:${gaeVersion}"
	compile "com.google.appengine:appengine-api-labs:${gaeVersion}"
	compile "com.google.appengine:appengine-api-stubs:${gaeVersion}"
	compile "com.google.appengine:appengine-testing:${gaeVersion}"
	compile "javax.servlet:servlet-api:2.5"
	compile "javax.servlet.jsp:jsp-api:2.2"
	compile "cglib:cglib-nodep:2.2"
	compile "org.objenesis:objenesis:1.2"
}

idea {
	project {
		jdkName "1.6"
		ipr {
			withXml { provider ->
				def node = provider.asNode()
				node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
			}
		}
	}
}

tasks.withType(Upload) { // map the groovy configuration to compile in the pom
	repositories.withType(MavenResolver) {
		pom.scopeMappings.addMapping(1, configurations.groovy, "compile")
	}
}

modifyPom {
	project {
		name 'Gaelyk Spock'
		packaging 'jar'
		description 'Spock specification support for Gaelyk lightweight toolkit for Google App Engine.'
		url 'https://github.com/gaelyk/gaelyk-spock'

		scm {
			url 'scm:git://github.com/gaelyk/gaelyk-spock.git'
			connection 'scm:git://github.com/gaelyk/gaelyk-spock.git'
			developerConnection 'scm:git://github.com/gaelyk/gaelyk-spock.git'
		}

		licenses {
			license {
				name 'The Apache Software License, Version 2.0'
				url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
				distribution 'repo'
			}
		}

		developers {
			developer {
				id 'vladimirorany'
				name 'Vladimir Orany'
			}
			developer {
				id 'marcoVermeulen'
				name 'Marco Vermeulen'
			}
            developer {
                id 'erdi'
                name 'Marcin Erdmann'
            }
		}
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '1.2'
}
