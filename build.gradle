apply plugin: "groovy"
apply plugin: "eclipse"
apply plugin: "maven"
apply plugin: "nexus"

/**
 * http://evgeny-goldin.com/wiki/Gradle-CodeNarc-plugin
 */
ext.codenarcPriority1Violations = 0     // Work
ext.codenarcPriority2Violations = 227   // in
ext.codenarcPriority3Violations = 983   // progress ..
ext.codenarcRuleSetFiles        = [ 'codenarc.groovy' ]
ext.codenarcSources             = [ 'src/main', 'src/test' ]
ext.codenarcReportType          = 'xml'
ext.codenarcReportFile          = 'codenarc.xml'
apply from: 'https://raw.github.com/evgeny-goldin/gradle-plugins/master/codenarc/CodeNarc.gradle'


def compatibilityVersion = 1.6
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

archivesBaseName = "gaelyk-spock"
version = "0.4-SNAPSHOT"
group = 'org.gaelyk'

buildscript {
    repositories {
        mavenCentral()
    }
    
    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.2'
    }
}

repositories {
	mavenCentral()
    mavenRepo name: 'sonatype-snapshots', url: 'https://oss.sonatype.org/content/repositories/snapshots'
}

dependencies {
	def gaeVersion = "1.7.3"
	groovy "org.codehaus.groovy:groovy-all:2.0.4", {
        force = true
    }
	compile "org.spockframework:spock-core:0.7-groovy-2.0", {
        exclude group: 'org.codehaus.groovy'
    }
	compile "org.gaelyk:gaelyk:2.0-SNAPSHOT"
	compile "com.google.appengine:appengine-api-1.0-sdk:${gaeVersion}"
	compile "com.google.appengine:appengine-api-labs:${gaeVersion}"
	compile "com.google.appengine:appengine-api-stubs:${gaeVersion}"
	compile "com.google.appengine:appengine-testing:${gaeVersion}"
	compile "javax.servlet:servlet-api:2.5"
	compile "cglib:cglib-nodep:2.2"
	compile "org.objenesis:objenesis:1.2"
}

tasks.withType(Upload) { // map the groovy configuration to compile in the pom
    repositories.withType(MavenResolver) {
        pom.scopeMappings.addMapping(1, configurations.groovy, "compile")
    }
}

modifyPom {
    project {
        name 'Gaelyk Spock'
        packaging 'jar'
        description 'Spock specification support for Gaelyk lightweight toolkit for Google App Engine.'
        url 'https://github.com/gaelyk/gaelyk-spock'

        scm {
            url 'scm:git://github.com/gaelyk/gaelyk-spock.git'
            connection 'scm:git://github.com/gaelyk/gaelyk-spock.git'
            developerConnection 'scm:git://github.com/musketyr/gaelyk-spock.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'vladimirorany'
                name 'Vladimir Orany'
            }
            developer {
                id 'marcoVermeulen'
                name 'Marco Vermeulen'
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.2'
}

boolean skipSigningVal = !hasProperty('skipSigning') || skipSigning != 'true'
nexus {
    sign = skipSigningVal
}
